#
    setup.singkong
    Account setup
    part of Bakwan: simple accounting software
    
    (c) Noprianto (nop@noprianto.com), 2023
    (c) Bakwan developers, 2023
;

load_module("ui_calendar")

var _default_fiscal_start = fn() {
    var n = part(@)
    return [
        n[0],
        1,
        format_date(date([n[0], 1, 1]))
    ]
}

var _default_fiscal_end = fn() {
    var n = part(@)
    var z = len(days_of_month(n[0], 12))
    return [
        n[0],
        12,
        format_date(date([n[0], 12, z]))
    ]
}

var setup_form = fn() {
    reset()
    title(_("title_setup"))
    closing(_("confirm_quit"), _("title_confirm"))

    var g = component("grid", "")

    var bs = c_btn_(_("cmd_save"), 0, __("cmd_save"))
    var bq = c_btn_(_("cmd_quit"), 5, __("cmd_quit"))

    var tc = c_text(_("label_company"))

    var gf = component("grid", "")
    config(gf, "border", _("label_fiscal_year"))
    var ts = component("label", upper(_("_start"), true))
    var te = component("label", upper(_("_end"), true))

    var dfs = _default_fiscal_start()
    var ds = create_date_picker(dfs[0], dfs[1])
    config(ds[1], "contents", dfs[2])
    var dfe = _default_fiscal_end()
    var de = create_date_picker(dfe[0], dfe[1])
    config(de[1], "contents", dfe[2])

    grid_add(gf, ts, 0, 1, 1, 1, 0.1, 1, 0, 1)
    grid_add(gf, ds[0], 1, 1, 1, 1, 0.9, 1, 1, 1)
    grid_add(gf, te, 2, 1, 1, 1, 0.1, 1, 0, 1)
    grid_add(gf, de[0], 3, 1, 1, 1, 0.9, 1, 1, 1)

    var gp = component("grid", "")

    grid_add(g, tc[0], 0, 0, 4, 1, 1, 1, 1, 1)
    grid_add(g, gf, 0, 1, 4, 1, 1, 1, 1, 1)
    grid_add(g, gp, 0, 2, 4, 1, 1, 20, 3, 0)
    grid_add(g, bq, 1, 3, 1, 1, 1, 1, 0, 2)
    grid_add(g, bs, 2, 3, 1, 1, 1, 1, 0, 1)
    add(g)

    show()

    event(bs, fn() {
        var _tc = trim(get(tc[1], "contents"))
        if (empty(_tc)) {
            s_text_label(tc, _("error_company_name"), 1)
        } else {
            config(tc[1], "contents", _tc)
            s_text_label(tc, " ", 0)
        }
    })

    event(bq, fn() {
        frame_close()
    })
}
