#
    bakwan.singkong
    
    part of Bakwan: simple accounting software
    
    Authors:
    Noprianto (nop@noprianto.com)
    Budiman (Budyzhang778@gmail.com)
;

require(8.6)

load_file_or_resource("account.singkong")
load_file_or_resource("system.singkong")
load_file_or_resource("ui.singkong")
load_file_or_resource("setup.singkong")
load_file_or_resource("message.singkong")
load_file_or_resource("config.singkong")

var app_name = "Bakwan"

reset()
title(app_name)
show()

var frame_info = frame()
var ctx = {
    "name": app_name,

    "db": null,
    "db_type": "",
    "db_host": "",
    "db_name": "",

    "user": null,
    "user_id": null,
    "group_id": null,
    "permission": null
,
    "dialog_width": 400,
    "dialog_height": 240,
    "dialog_width_2": frame_info[0] * 0.9,
    "dialog_height_2": frame_info[1] * 0.9,
}

var init_ui = fn(ctx) {
    closing(_("confirm_quit"), _("title_confirm"))

    var status_db = upper(_("_db_connected"), true) + " " + 
        "(" + ctx["db_type"] + ") " + ctx["db_name"] 
    statusbar(0, status_db, true)

    timer(1000, fn() {
        statusbar(1, string(@), true)
    })

    var r = setup_form(ctx)
}

var conf = read_config(ctx)
var conf_status = repeat {
    if (empty(conf)) {
        var r = config_form(ctx)
        if (r["config"] == true | r["command"] == "close") {
            var conf = read_config(ctx)
            return r
        }
    } else {
        return true
    }
}

if (empty(conf)) {
    frame_close()
} else {
    var db = get_db(ctx)
    if (db == null) {
        var m = _("error_db_connect") + lf() + _("confirm_config_reset")
        var r = confirm(m, _("title_confirm"))
        if (r == "OK") {
            if (properties_write(conf_path, {})) {
                message(_("info_app_restart"), _("title_info"))
            } else {
                message(_("error_config_write"), _("title_error"))
            }
        }
        frame_close()
    } else {
        set(ctx, "db", db)
        set(ctx, "db_type", conf["db_type"])
        set(ctx, "db_host", conf["db_host"])
        set(ctx, "db_name", conf["db_name"])

        if (is_new(ctx)) {
            var r = system_init_db(ctx)
            if (r == null) {
                message(_("error_db_init"), _("title_error"))
                frame_close()
            } 
            message(
                _("success_db_init") + lf() +
                _("info_app_user") + ": " + lf() +
                upper(_("_username"), true) + ": " + 
                db_system_default_user + lf() +
                upper(_("_password"), true) + ": " + 
                db_system_default_password + lf(),
                _("title_info")
            )
        }

        var r_auth = repeat {
            var r = login_dialog(
                _("title_login"),
                upper(_("_username"), true),
                upper(_("_password"), true)
            )
            if (len(r) > 1) {
                var u = r[0]
                var p = r[1]
                var r_auth = system_auth(ctx, u, p)
                if (len(r_auth) == 3) {
                    return r_auth
                } else {
                    message(
                        _("error_auth"),
                        _("title_info")
                    )
                }
            } else {
                return []
            }
        }
        if (len(r_auth) == 3) {
            set(ctx, "user_id", r_auth[0])
            set(ctx, "group_id", r_auth[1])
            set(ctx, "user", r_auth[2])
            init_ui(ctx)
        } else {
            frame_close()
        }
    }
}
